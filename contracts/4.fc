{-
  TASK 4 - Caesar Cipher
  Implement a Caesar cipher encryption and decryption functions.
  The input is a string (https://docs.ton.org/develop/smart-contracts/guidelines/internal-messages#simple-message-with-comment)
  where the text is encrypted in a cell (after 32bit 00000000 prefix flag), the algorithm rotates the characters and the last ASCII
  character should go to the beginning. Return new text encoded in cell.
-}

forall X -> X tpop(tuple t) asm "TPOP";
forall X -> (tuple, X) ~tpop(tuple t) asm "TPOP";
int tlen(tuple t) asm "TLEN";

const int au = "A"u;
const int zu = "Z"u;
const int al = "a"u;
const int zl = "z"u;

() recv_internal() {
}

;; testable
(cell) caesar_cipher_encrypt(int shift, cell text) method_id {
  slice s = text.begin_parse();
  s~skip_bits(32);
  var b = begin_cell().store_int(0, 32);
  
  var builders = empty_tuple();
  
  while(s.slice_bits()) {
    int char = s~load_int(8);
    if ((char >= au) & (char <= zu)) {
      b = b.store_int(((char - au + shift + 26) % 26) + au, 8);
    }
    else {
      if ((char >= al) & (char <= zl)) {
        b = b.store_int(((char - al + shift + 26) % 26) + al, 8);
      }
      else {
        b = b.store_int(char, 8);
      }
    }
    if (s.slice_data_empty?()) {
      if (s.slice_refs()) {
        builders~tpush(b);
        s = s~load_ref().begin_parse();
        b = begin_cell();
      }
    }
  }
  
  while (builders.tlen()) {
    var c = b.end_cell();
    b = builders~tpop();
    b = b.store_ref(c);
  }

  return b.end_cell();
}

;; testable
(cell) caesar_cipher_decrypt(int shift, cell text) method_id {
  return caesar_cipher_encrypt(- shift, text);
}

