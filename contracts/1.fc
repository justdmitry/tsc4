{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";
(cell, int) udict_add_ref?(cell dict, int key_len, int index, cell value) asm(value index dict key_len) "DICTUADDREF";

() recv_internal() {
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  var visited = null();
  tuple stack = null();
  stack~push_back(tree);
  do {
    tree = stack~pop_back();
    var hsh = cell_hash(tree);
    if (hsh == hash) { 
      return tree;
    }
    if (visited~udict_add_ref?(256, hsh, tree)) {
      slice s = tree.begin_parse();
      repeat (s.slice_refs()) {
        stack~push_back(s~load_ref());
      }
    }
  } until (stack.is_null());

  return begin_cell().end_cell();
}
