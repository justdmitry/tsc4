{-
  TASK 4 - Caesar Cipher
  Implement a Caesar cipher encryption and decryption functions.
  The input is a string (https://docs.ton.org/develop/smart-contracts/guidelines/internal-messages#simple-message-with-comment)
  where the text is encrypted in a cell (after 32bit 00000000 prefix flag), the algorithm rotates the characters and the last ASCII
  character should go to the beginning. Return new text encoded in cell.
-}

const int au = "A"u;
const int zu = "Z"u;
const int al = "a"u;
const int zl = "z"u;
const int crcl = 26;

() recv_internal() {
}

(cell) copy_and_shift(int shift, slice src, builder dest) {
  while(src.slice_bits()) {
    int char = src~load_int(8);
    if ((char >= au) & (char <= zu)) {
      char = ((char - au + shift) % crcl) + au;
    }
    else {
      if ((char >= al) & (char <= zl)) {
        char = ((char - al + shift) % crcl) + al;
      }
    }
    dest = dest.store_int(char, 8);
    if (src.slice_data_empty?()) {
      if (src.slice_refs()) {
        dest = dest.store_ref(copy_and_shift(shift, src~load_ref().begin_parse(), begin_cell()));
      }
    }
  }
  return dest.end_cell();
}

;; testable
(cell) caesar_cipher_encrypt(int shift, cell text) method_id {
  slice s = text.begin_parse();
  s~skip_bits(32);
  builder b = begin_cell().store_int(0, 32);
  return copy_and_shift(shift, s, b);
}

;; testable
(cell) caesar_cipher_decrypt(int shift, cell text) method_id {
  return caesar_cipher_encrypt(crcl - shift, text);
}

