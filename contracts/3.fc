{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

() recv_internal() {
}


int bitsize(int x) asm "BITSIZE";
int ubitsize(int x) asm "UBITSIZE";
int pow2(int x) asm "POW2";
int startsWith?(slice src, slice val) asm "SDPFXREV";
(slice, int) ~beginsWith?(slice src, slice val) asm "SDBEGINSXQ";
forall X -> X tpop(tuple t) asm "TPOP";
forall X -> (tuple, X) ~tpop(tuple t) asm "TPOP";
int tlen(tuple t) asm "TLEN";
int min(int x, int y) asm "MIN";

global int len1;
global int len2;
global slice slice1;
global slice slice2;

global slice source;
global slice sourceBuffer;
global int eof;
global tuple builders;
global builder current;

() getData() impure {

  if (source.slice_empty?()) {
    eof = true;
    return ();
  }

  if (source.slice_data_empty?()) {
    source = source~load_ref().begin_parse();
    return getData();
  }

  var b = begin_cell();
  
  ifnot (null?(sourceBuffer)) {
    b = b.store_slice(sourceBuffer);
  }
  
  if (source.slice_bits() >= 400) {
    sourceBuffer = b.store_slice(source~load_bits(400)).end_cell().begin_parse();
    return ();
  }
  
  sourceBuffer = b.store_slice(source~load_bits(source.slice_bits())).end_cell().begin_parse();

  if (source.slice_empty?()) {
    eof = true;
  }
  
  getData();
}

() storeSlice2() impure inline {
  if ((current.builder_bits() + len2) > 1023) {
    builders = cons(current, builders);
    current = begin_cell();
  }
  current = current.store_slice(slice2);
}

() storeBit(int val) impure inline {
  if (current.builder_bits() == 1023) {
    builders = cons(current, builders);
    current = begin_cell();
  }
  current = current.store_uint(val, 1);
}

() storeTail() impure inline_ref {
  builders = cons(current, builders);
  current = begin_cell().store_slice(sourceBuffer);
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {

  source = linked_list.begin_parse();
  if (source.slice_empty?()) {
    return begin_cell().end_cell();
  }

  len1 = ubitsize(flag);
  slice1 = begin_cell().store_uint(flag, len1).end_cell().begin_parse();

  len2 = ubitsize(value);
  slice2 = begin_cell().store_uint(value, len2).end_cell().begin_parse();

  builders = null();
  current = begin_cell();
  sourceBuffer = null();
  eof = 0;
  
  getData();
  do {
    if (sourceBuffer.slice_bits() >= len1) {
      if (sourceBuffer~beginsWith?(slice1)) {
        storeSlice2();        
      }
      else {
        storeBit(sourceBuffer~load_uint(1));
      }
    }
    else {
      if (eof) {
        storeTail();
        sourceBuffer = null();
      }
      else {
        getData();
      }
    }
    
  } until (null?(sourceBuffer));

  ;;;;;;;;;;;;;;;;;;;;
  ;; build final cell
  ;;;;;;;;;;;;;;;;;;;;
  var b = current;
  while (~ null?(builders)) {
    var c = b.end_cell();
    (b, builders) = uncons(builders);
    b = b.store_ref(c);
  }

  var dest = b.end_cell();
  
  return dest;
}

