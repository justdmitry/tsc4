{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

() recv_internal() {
}

global int hashg;

(cell) find_branch_by_hash_impl(cell tree) {
  slice s = tree.begin_parse();
  repeat (s.slice_refs()) {
    cell r = s~load_ref();
    if (hashg == cell_hash(r)) { return r; }
    cell b = find_branch_by_hash_impl(r);
    if (~ null?(b)) { return b; }
  }
  return null();
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  if (hash == cell_hash(tree)) { return tree; }
  hashg = hash;
  cell branch = find_branch_by_hash_impl(tree);
  return null?(branch) ? begin_cell().end_cell() : branch;
}
