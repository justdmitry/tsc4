{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";
(cell) preload_ref_var(slice s, int num) asm "PLDREFVAR";

() recv_internal() {
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  tuple stack = null();
  stack~push_back(tree);
  do {
    tree = stack~pop_back();
    if (cell_hash(tree) == hash) { 
      return tree;
    }
    slice s = tree.begin_parse();
    var cnt = s.slice_refs();
    while(cnt) {
      cnt -= 1;
      stack~push_back(s.preload_ref_var(cnt));
    }
  } until (stack.is_null());

  return begin_cell().end_cell();
}
