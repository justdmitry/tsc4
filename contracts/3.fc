{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

() recv_internal() {
}


int bitsize(int x) asm "BITSIZE";
int ubitsize(int x) asm "UBITSIZE";
int pow2(int x) asm "POW2";
int startsWith?(slice src, slice val) asm "SDPFXREV";
(slice, int) ~beginsWith?(slice src, slice val) asm "SDBEGINSXQ";
forall X -> X tpop(tuple t) asm "TPOP";
forall X -> (tuple, X) ~tpop(tuple t) asm "TPOP";
int tlen(tuple t) asm "TLEN";
int min(int x, int y) asm "MIN";
int builder_remaining_bits(builder b) asm "BREMBITS";
int builder_canstore_bits?(builder b, int count) asm "BCHKBITSQ";
int slice_canload_bits?(slice s, int count) asm "SCHKBITSQ";

global slice source;

(int, slice) getData(slice tail, int len) impure {

  if (source.slice_empty?()) {
    return (false, tail);
  }
  
  var b = begin_cell();
  
  ifnot (null?(tail)) {
    b = b.store_slice(tail);
  }

  while (true) {
    if (b.builder_bits() >= len)
    {
      return (true, b.end_cell().begin_parse());
    }
    
    var havebits = source.slice_bits();
    if (havebits) {
      var canload = min(b.builder_remaining_bits(), havebits);
      b = b.store_slice(source~load_bits(canload));
    }
    else {
      if (source.slice_refs()) {
        source = source~load_ref().begin_parse();
      }
      else {
        return (false, b.end_cell().begin_parse());
      }
    }
  }
  
  return (false, tail);
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {

  source = linked_list.begin_parse();
  if (source.slice_empty?()) {
    return begin_cell().end_cell();
  }

  var len1 = ubitsize(flag);
  var slice1 = begin_cell().store_uint(flag, len1).end_cell().begin_parse();

  var len2 = ubitsize(value);
  var 1023minuslen2 = 1023 - len2;
  var slice2 = begin_cell().store_uint(value, len2).end_cell().begin_parse();

  var builders = null();
  var current = begin_cell();
  
  var (haveMore, data) = getData(null(), len1);
  while (haveMore) {
    if (data~beginsWith?(slice1)) {
      current = current.store_slice(slice2);
    }
    else {
      current = current.store_uint(data~load_uint(1), 1);
    }
    
    ifnot (current.builder_canstore_bits?(len2)) {
      builders = cons(current, builders);
      current = begin_cell();
    }
    
    ifnot (data.slice_canload_bits?(len1)) {
      (haveMore, data) = getData(data, len1);
    }
  }

  builders = cons(current, builders);
  current = begin_cell().store_slice(data);

  ;;;;;;;;;;;;;;;;;;;;
  ;; build final cell
  ;;;;;;;;;;;;;;;;;;;;
  var b = current;
  while (~ null?(builders)) {
    var c = b.end_cell();
    (b, builders) = uncons(builders);
    b = b.store_ref(c);
  }

  var dest = b.end_cell();
  
  return dest;
}

