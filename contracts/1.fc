{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

forall X -> X tpop(tuple t) asm "TPOP";
forall X -> (tuple, X) ~tpop(tuple t) asm "TPOP";
int tlen(tuple t) asm "TLEN";

() recv_internal() {
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  var nodes = empty_tuple();
  do {
    if (hash == cell_hash(tree)) { 
      return tree;
    }
    slice s = tree.begin_parse();
    repeat (s.slice_refs()) {
      nodes~tpush(s~load_ref());
    }
    tree = (nodes.tlen() == 0) ? null() : nodes~tpop();
  } until (null?(tree));
  return begin_cell().end_cell();
}
