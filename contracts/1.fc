{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

forall X -> X tpop(tuple t) asm "TPOP";
forall X -> (tuple, X) ~tpop(tuple t) asm "TPOP";
int tlen(tuple t) asm "TLEN";

() recv_internal() {
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  var nodes = empty_tuple();
  nodes~tpush(tree);
  while (nodes.tlen()) {
    slice s = nodes~tpop().begin_parse();
    repeat (s.slice_refs()) {
      tree = s~load_ref();
      if (hash == cell_hash(tree)) { 
        return tree;
      }
      nodes~tpush(tree);
    }
  }
  return begin_cell().end_cell();
}
