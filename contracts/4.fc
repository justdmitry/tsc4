{-
  TASK 4 - Caesar Cipher
  Implement a Caesar cipher encryption and decryption functions.
  The input is a string (https://docs.ton.org/develop/smart-contracts/guidelines/internal-messages#simple-message-with-comment)
  where the text is encrypted in a cell (after 32bit 00000000 prefix flag), the algorithm rotates the characters and the last ASCII
  character should go to the beginning. Return new text encoded in cell.
-}

const int au = "A"u;
const int zu = "Z"u;
const int al = "a"u;
const int zl = "z"u;
const int crcl = 26;

global int globshift;

() recv_internal() {
}

(cell) copy_and_shift(slice src) {
  var dest = begin_cell();
  while(src.slice_bits()) {
    int char = src~load_int(8);
    if ((char >= al) & (char <= zl)) {
      char = ((char - al + globshift) % crcl) + al;
    }
    else {
      if ((char >= au) & (char <= zu)) {
        char = ((char - au + globshift) % crcl) + au;
      }
    }
    dest = dest.store_int(char, 8);
  }
  if (src.slice_refs()) {
    dest = dest.store_ref(copy_and_shift(src~load_ref().begin_parse()));
  }
  return dest.end_cell();
}

;; testable
(cell) caesar_cipher_encrypt(int shift, cell text) inline method_id {
  globshift = shift % crcl;
  ifnot (globshift) {
    return text;
  }
  return copy_and_shift(text.begin_parse());
}

;; testable
(cell) caesar_cipher_decrypt(int shift, cell text) method_id {
  return caesar_cipher_encrypt(crcl - shift, text);
}

