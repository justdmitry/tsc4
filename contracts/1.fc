{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

forall X -> X tpop(tuple t) asm "TPOP";
forall X -> (tuple, X) ~tpop(tuple t) asm "TPOP";
int tlen(tuple t) asm "TLEN";

const slice one = "1"s;

() recv_internal() {
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  if (hash == cell_hash(tree)) { 
    return tree;
  }
  var nodes = empty_tuple();
  var dict = new_dict();
  do {
    slice s = tree.begin_parse();
    repeat (s.slice_refs()) {
      tree = s~load_ref();
      var thash = cell_hash(tree);
      if (hash == thash) { 
        return tree;
      }
      var (_, found) = dict.udict_get?(256, thash);
      if (~ found) {
        dict~udict_set(256, thash, one);
        nodes~tpush(tree);
      }
    }
    tree = nodes.tlen() ? nodes~tpop() : null();
  } until (null?(tree));
  return begin_cell().end_cell();
}
